import org.gradle.internal.os.OperatingSystem

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.71'
    id 'org.javamodularity.moduleplugin' version '1.6.0'
    id 'org.beryx.jlink' version '2.17.4'
    id 'org.jetbrains.dokka' version '0.10.1'
}

group 'com.kneelawk'
version '0.0.1-SNAPSHOT'
sourceCompatibility = 14

mainClassName = "$moduleName/com.kneelawk.modfinder.ModFinderAppKt"

project.ext.tornadofxVersion = '1.7.20'
project.ext.jfxVersion = '14'

switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        project.ext.jfxPlatform = "linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.jfxPlatform = "mac"
        break
    case OperatingSystem.WINDOWS:
        project.ext.jfxPlatform = "win"
        break
}

repositories {
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "no.tornado:tornadofx:$tornadofxVersion"

    implementation "org.openjfx:javafx-base:$jfxVersion:$jfxPlatform"
    implementation "org.openjfx:javafx-controls:$jfxVersion:$jfxPlatform"
    implementation "org.openjfx:javafx-graphics:$jfxVersion:$jfxPlatform"
}

jlink {
    extraDependenciesPrefixes.add("javafx")

    jpackage {
        skipInstaller = true
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}
